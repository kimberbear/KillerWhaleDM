ACTOR FXBase : Actor 
{
	+NOINTERACTION
	+NOBLOCKMAP
	+NOGRAVITY
	+NOTONAUTOMAP
	+CLIENTSIDEONLY
	+NONETID
	+THRUACTORS
	+NOTELEPORT
	+DONTSPLASH
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	}
}

ACTOR CWDM_bulletPuff : FXBase
{

	RenderStyle "Normal"
	Alpha 0.8
	xScale 0.25
	yScale 0.25
	-NOINTERACTION
	+FORCEXYBILLBOARD
	+SKYEXPLODE
	+ALWAYSPUFF
	+PUFFONACTORS
	+HITTRACER
	Decal "BLDCL"
	States
	{
	Spawn:
		//TNT1 A 0 NODELAY A_SetRoll(random(0,360))
		TNT1 A 0 A_JumpIfInventory("QuadDamage", 0, "Crit", AAPTR_TARGET)
		BLPF ABCDE 2 Bright 
		goto death
		//TNT1 A 0
		//goto tracer
	tracer:
		TNT1 A 0 A_SpawnItemEX("CWDM_Tracer", 0, 0, 0, 0, 0, 0, 0, SXF_SETMASTER|SXF_TRANSFERPOINTERS)
		stop
	Death:
		TNT1 A 1
		stop
	}
}

ACTOR CWDM_Blood : FXBase Replaces Blood
{
	scale 0.7
	+FORCEXYBILLBOARD
	States
	{
	Spawn:
		TNT1 AAA 0 A_SpawnItemEX("CWDM_BloodDroplet", 0, 0, 0, random(-6, 6), 0, random(0, 6), random(0,360))
		Stop
	}
}

Actor CWDM_OneBlood: CWDM_BLood
{
	States
	{
	Spawn:
		TNT1 AA 0 A_SpawnItemEX("CWDM_BloodDroplet", 0, 0, 0, random(-6, 6), 0, random(0, 6), random(0,360))
		Stop
	}
}

ACTOR CWDM_BloodSplatter : CWDM_Blood Replaces BloodSplatter
{
}

ACTOR CWDM_BloodDroplet : FXBase
{
    Gravity 0.6
	Scale 0.4
	-NOINTERACTION
	-NOGRAVITY
	States
	{
    Spawn:
		BLUD C 1 A_SpawnItemEX("CWDM_BloodTrail", random(-1,1), 0, random(-1,1), 0, 0, 0, random(0,360))
		TNT1 A 0 A_CheckFloor("Land")
		Loop
	Land:
		TNT1 A 5
		Stop
	}
}

ACTOR CWDM_BloodTrail : FXBase
{
	Scale 0.2
	States
	{
    Spawn:
		BLUD CBA 3
		Stop
	}
}

actor CWDM_gib : CWDM_BloodDroplet
{
	Scale 1
    States
    {
    Spawn:
        BON1 A 1 A_SpawnItemEX("CWDM_BloodTrail", random(-1,1), 0, random(-1,1), 0, 0, 0, random(0,360))
		TNT1 A 0 A_CheckFloor("Land")
		Loop
	Land:
		TNT1 A 0 A_SpawnItemEX("CWDM_Blood",0,0,0,0,0,0,random(0,360))
		TNT1 A 5
		Stop
	}
}

actor CWDM_explosion : FXBase
{
	States
	{
	Spawn:
		EXP2 ABCDEFGH 2 BRIGHT
		stop
	}
}

actor CWDM_smallExplosion : CWDM_explosion
{
	Scale 0.5
}

actor CWDM_dustCloud : FXBase
{
	alpha 0.5
	renderStyle translucent
	scale 0.5
	States
	{
	Spawn:
		DUST AAAAA 5 A_fadeout(0.1)
		Stop
	}
}