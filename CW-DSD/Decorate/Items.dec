actor CWDM_healthBonus : Health 15300
{
//$Category Health and Armor
//$Title "Health Bonus"
//$Sprite BON1A0
	Inventory.PickupSound "items/eat"
	Inventory.PickupMessage "Snacked on a shrimp! +2 HP !"
    Inventory.Amount 2 
	Inventory.MaxAmount 200
	States
	{
	spawn:
		BON1 a -1
		loop
	}
}


actor CWDM_medikit : Health 15302
{
//$Category Health and Armor
//$Title "Medikit"
//$Sprite MEDIA0
	Inventory.Amount 50
	Inventory.MaxAmount 200
	Inventory.PickupSound "items/eat"
	Inventory.PickupMessage "Chowed down a bloody bucket of chum! +50 HP !"
	States
	{
	spawn:
		MEDI a -1
		stop
	}
}

actor CWDM_mediumArmor : BasicArmorBonus  15306
{
//$Category Health and Armor
//$Title "Armor"
//$Sprite ARM1A0
	+INVENTORY.AUTOACTIVATE
	Inventory.PickupSound "items/armor1"
	Inventory.PickupMessage "Booze! +100 AP"
	Armor.SavePercent 75
	Armor.MaxSaveAmount 200
	Armor.SaveAmount 100

	States
	{
	spawn:
		ARM1 ab 4
		loop
	}
}


actor CWDM_smallHealthBonus : Health
{
    Inventory.Amount 25 // heals 25 HP
	Inventory.MaxAmount 200
}

actor CWDM_smallArmorBonus : BasicArmorBonus
{
    Armor.SaveAmount 25
	Armor.SavePercent 75
    Armor.MaxSaveAmount 200
}

actor CWDM_deathPack : CustomInventory
{
	Inventory.PickupSound "ammoget"
    Inventory.PickupMessage "+25 HP +25 AP"
    Inventory.Icon "BPAKA0"
    +INVENTORY.AUTOACTIVATE   // auto-use when picked up
	+INVENTORY.ALWAYSPICKUP
    +INVENTORY.NEVERRESPAWN   
	radius 20

    States
    {
	spawn:
		MEDI a -1
		stop
    Pickup:
        TNT1 A 0 A_GiveInventory("CWDM_smallHealthBonus", 1) // give health
        TNT1 A 0 A_GiveInventory("CWDM_smallArmorBonus", 1)  // give armor
        Stop
    }
}
actor QuadDamage : Powerup
{
    DamageFactor "Normal", 4
    Inventory.Icon "QUADC0"
}

actor CWDM_skullOfPower : PowerupGiver
{
    Powerup.Type "QuadDamage"   
    Powerup.Duration 1050
    Powerup.Color "Gold", 0.25

    +Inventory.AUTOACTIVATE
    +Inventory.FANCYPICKUPSOUND
    +FLOATBOB

    States
    {
    Spawn:
        QUAD ABCDEFGH 4 bright
        Loop
    }
}

actor CWDM_skullOfPowerPickup : CustomInventory 17002
{
//$Category Powerup
//$Title "Quad Damage"
//$Sprite QUADC0
    +NOGRAVITY
    +INVENTORY.ALWAYSPICKUP
    Inventory.Pickupsound "powerup"
    Inventory.PickupMessage "Quad Damage!"
    Inventory.RespawnTics  2100 
	scale 2

    States
    {
    Spawn:
        QUAD ABCDEFGH 4 bright
        Loop

    Pickup:
        TNT1 A 0 A_GiveInventory("CWDM_skullOfPower", 1)
        Stop
    }
}

actor CWDM_bossPickup : CustomInventory
{
    +INVENTORY.NEVERRESPAWN
    +INVENTORY.AUTOACTIVATE   // auto-use when picked up
	+INVENTORY.ALWAYSPICKUP
	scale 2.0
	radius 20
}

actor CWDM_stanBossPickup : CWDM_bossPickup 17003
{
//$Category Powerup
//$Title "Stanley Boss Pickup"
//$Sprite SPIKA0
    States
    {
    Spawn:
        SPIK A 4 bright
        Loop

    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("stanPickup")
        Stop
    }
}

actor CWDM_EARLBossPickup : CWDM_bossPickup 17005
{
//$Category Powerup
//$Title "EARL Boss Pickup"
//$Sprite ERPKA0
    States
    {
    Spawn:
        ERPK A 4 bright
        Loop

    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("earlPickup")
        Stop
    }
}

actor CWDM_smasherBossPickup : CWDM_bossPickup 17004
{
//$Category Powerup
//$Title "Sunken Smasher Boss Pickup"
//$Sprite SMPKA0
    States
    {
    Spawn:
        SMPK A 4 bright
        Loop

    Pickup:
        TNT1 A 0 ACS_NamedExecuteAlways("smasherPickup")
        Stop
    }
}


actor CWDM_randomBossPickup : CWDM_bossPickup 17006
{
//$Category Powerup
//$Title "Random Boss Pickup"
//$Sprite ERPKA0
    States
    {
    Spawn:
		SPIK A 4 BRIGHT
        ERPK A 4 BRIGHT
		SMPK A 4 BRIGHT
        Loop

    Pickup:
        TNT1 A 0 A_Jump(256 , "StanPickup", "EARLPickup", "SmasherPickup")
        stop
	StanPickup:
        TNT1 A 0 ACS_NamedExecuteAlways("stanPickup")
        Stop
	EARLPickup:
        TNT1 A 0 ACS_NamedExecuteAlways("earlPickup")
        Stop
	SmasherPickup:
        TNT1 A 0 ACS_NamedExecuteAlways("smasherPickup")
        Stop
    }
}