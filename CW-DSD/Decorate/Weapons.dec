ACTOR CWDM_Shotgun: CWDM_weapon 
{
	Obituary "$OBSHGUN"
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	Weapon.SlotNumber 2
	Xscale 0.5
	Yscale 0.5
	AttackSound "weapons/shotgunfire"
	Tag "Shotgun"
	States
	{
	Spawn:
		SGIP A -1
		Stop
	Ready:
		SHGN A 1 A_WeaponReady
		Loop
	Select:
		SHGN A 1 A_Raise
		Loop
	Deselect:
		SHGN A 1 A_Lower
		Loop
	Fire:
		SHGN B 0 A_Light2
		SHGN B 0 A_Quake(2,5,0,5)
		SHGN B 1 BRIGHT A_FireBullets(10,6,8,8,"CWDM_bulletPuff",FBF_NORANDOM)
		SHGN B 0 A_Light0
		SHGN CCCDDA 4
		Goto Ready
	}
}

actor CWDM_rifleShotCount : Inventory
{
	Inventory.MaxAmount 24
	Inventory.InterHubAmount 0
	+INVENTORY.UNDROPPABLE
}

ACTOR CWDM_rifle: CWDM_weapon 
{
	Obituary "$OBRIFLE"
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	Weapon.SlotNumber 2
	Xscale 0.5
	Yscale 0.5
	AttackSound "weapons/riflefire"
	Tag "Rifle"
	States
	{
	Spawn:
		SGIP A -1
		Stop
	Ready:
		RFLE A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Select:
		RFLE A 1 A_Raise
		Loop
	Deselect:
		RFLE A 1 A_Lower
		Loop
	Fire:
		RFLE A 0 A_JumpIfInventory("CWDM_rifleShotCount",24,"Reload")
		RFLE A 0 A_GiveInventory("CWDM_rifleShotCount", 1)
		RFLE B 0 A_Light2
		RFLE B 0 A_Quake(1,5,0,5)
		RFLE B 1 BRIGHT A_FireBullets(2,2,1,15,"CWDM_bulletPuff",FBF_NORANDOM )
		RFLE B 0 A_Light0
		RFLE CCA 1
		RFLE A 0 A_ReFire
		Goto Ready
	Reload:
		RFLE A 0 A_TakeInventory("CWDM_rifleShotCount",24)

		RFLE ADEFGHI 2
		TNT1 A 0 A_PlaySound("weapons/weaponopen")
		RFLE JKLMNOP 2
		TNT1 A 0 A_PlaySound("weapons/weaponclose")
		RFLE QRST 2
		goto ready
	}
}

Actor FlareProjectile
{
    Damage 35
    Radius 8
    Height 8
    Speed 70
    Projectile
    Obituary "$OBFLARE"

    States
    {
    Spawn:
        FLAR A 2 BRIGHT A_SpawnItem("CWDM_dustCloud") 
        Loop

    Death:
        TNT1 A 0 A_Quake(3, 4, 0, 1024, "dsnone")
        TNT1 A 0 A_Quake(2, 8, 0, 1024, "dsnone")
        TNT1 A 0 A_Quake(1, 12, 0, 1024, "dsnone")
        TNT1 A 0 A_SpawnItem("CWDM_smallExplosion") //this was causing lag as it's super buggy and removed now
        TNT1 A 25 A_PlaySound("whale/flaregunexplode", CHAN_5)
        Stop
    }
}

ACTOR CWDM_brewGrenade: grenade
{
	//Health 5
	Radius 12
	Height 12
	Scale 0.5
	//BounceType "Hexen"
    Obituary "$OBBREW"
	BounceSound "weapons/dynamitebounce"
	+EXTREMEDEATH
	+FORCEXYBILLBOARD
	+SHOOTABLE
    States
    {
    Spawn:
		// what i do here should be a fucking crime
		// THIS IS OUTRAGEOUS
        WDNY A 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 
		WDNY A 0 A_CheckFloor("Land")
        WDNY B 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 
		WDNY B 0 A_CheckFloor("Land")
        WDNY C 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 
		WDNY C 0 A_CheckFloor("Land")
        WDNY D 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 
		WDNY D 0 A_CheckFloor("Land")
        WDNY E 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 
		WDNY E 0 A_CheckFloor("Land")
        WDNY F 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 
		WDNY F 0 A_CheckFloor("Land")
        WDNY G 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 
		WDNY G 0 A_CheckFloor("Land")
        WDNY H 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 
		WDNY H 0 A_CheckFloor("Land")
        Loop
	Land:
		WDNY IJK 3 BRIGHT A_SpawnItem("CWDM_dustCloud") 	
		loop
    Death:
        TNT1 A 0 A_Quake(3, 4, 0, 1024, "dsnone")
        TNT1 A 0 A_Quake(2, 8, 0, 1024, "dsnone")
        TNT1 A 0 A_Quake(1, 12, 0, 1024, "dsnone")
		TNT1 A 0 A_Explode
        TNT1 A 0 A_SpawnItem("CWDM_smallExplosion")
        TNT1 A 25 A_PlaySound("explosion", CHAN_5)
        Stop
    }
}

ACTOR CWDM_grenadeLauncher: CWDM_weapon 
{
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	//Weapon.AmmoType "KWDMshell"
	Weapon.SlotNumber 2
	Xscale 0.5
	Yscale 0.5
	AttackSound "weapons/grenadefire"
	Tag "Grenade Launcher"
	States
	{
	Spawn:
		SGIP A -1
		Stop
	Ready:
		GGUN A 1 A_WeaponReady
		Loop
	Select:
		GGUN A 1 A_Raise
		Loop
	Deselect:
		GGUN A 1 A_Lower
		Loop
	Fire:
		GGUN B 0 A_Light2
		GGUN B 0 A_Quake(2,5,0,5)
		FLAR B 0 A_PlayWeaponSound("weapons/grenadefire")
		GGUN B 1 a_firecustommissile("CWDM_brewGrenade")
		GGUN B 0 A_Light0
		GGUN BBCCA 3
		GGUN IDEF 3
		GGUN G 0 A_PlayWeaponSound("weapons/grenadeload")
		GGUN GHHGIA 3 
		Goto Ready
	}
}

ACTOR CWDM_flare: CWDM_weapon 
{
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	Weapon.SlotNumber 2
	Xscale 0.5
	Yscale 0.5
	AttackSound "weapons/grenadefire"
	Tag "Flare gun"
	States
	{
	Spawn:
		SGIP A -1
		Stop
	Ready:
		FLGN A 1 A_WeaponReady
		Loop
	Select:
		FLGN A 1 A_Raise
		Loop
	Deselect:
		FLGN A 1 A_Lower
		Loop
	Fire:
		FLGN B 0 A_Light2
		FLGN B 0 A_Quake(2,5,0,5)
		FLGN B 0 A_PlayWeaponSound("weapons/flarefire")
		FLGN B 1 a_firecustommissile("FlareProjectile")
		FLGN B 0 A_Light0
		FLGN BBCCA 3
		FLGN DEFGHIJK 2
		FLGN B 0 A_PlayWeaponSound("weapons/flareload")
		FLGN LMNOPQ 2
		Goto Ready
	}
}

actor CWDM_1887firedOnce : Inventory
{
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 0
	+INVENTORY.UNDROPPABLE
}

actor CWDM_dual1887: CWDM_weapon
{
	AttackSound "weapons/1887fire"
	Tag "Dual 1887s"
	States
	{
	Spawn:
		DWLA A -1
		Stop
	Ready:
		DWLA A 1 A_WeaponReady
		Loop
	Select:
		DWLA A 1 A_Raise
		Loop
	Deselect:
		DWLA A 1 A_Lower
		Loop
	Fire:
		DWLA A 0 A_JumpIfInventory("CWDM_1887firedOnce",1,"FireRight")
		DWLA A 1
		Goto FireLeft
	FireLeft:
		DWLA A 0 A_GiveInventory("CWDM_1887firedOnce", 1)
		DWLA A 0 A_Light2
		DWLA A 0 A_Quake(2,5,0,5)
		DWLL A 2 BRIGHT A_FireBullets(4,4,5,10,"CWDM_bulletPuff")
		DWLL B 0 A_Light0
		DWLL B 4
		DWLL DEDE 2
		DWLL BC 4
		goto Ready
	FireRight:
		DWLA A 0 A_TakeInventory("CWDM_1887firedOnce", 1)
		DWLA A 0 A_Light2
		DWLA A 0 A_Quake(2,5,0,5)
		DWLR A 2 BRIGHT A_FireBullets(4,4,5,10,"CWDM_bulletPuff")
		DWLR A 0 A_Light0
		DWLR B 4
		DWLR DEDE 2
		DWLR BC 4
		goto Ready
	}
}

actor CWDM_stanRevolver : CWDM_weapon
{
	AttackSound "weapons/revolverfire"
	Tag "Revolver"
	Obituary "$OBSTAN"
	States
	{
	Spawn:
		REVV A -1
		Stop
	Ready:
		REVV A 1 A_WeaponReady
		Loop
	Select:
		REVV A 1 A_Raise
		Loop
	Deselect:
		REVV A 1 A_Lower
		Loop
	Fire:
		REVV E 4
		REVV A 0 A_Light2
		REVV A 0 A_Quake(3,5,0,5)
		REVV B 1 BRIGHT A_FireBullets(2,2,2,50,"CWDM_bulletPuff",FBF_NORANDOM)
		REVV C 0 A_Light0
		REVV CDDCF 2
		REVV F 0 A_Refire
		goto ready
	AltFire:
		SBBF DEFG 2
		TNT1 A 4
		goto BrewHeld
	BrewHeld:
		TNT1 A 1
		TNT1 A 1 a_refire("BrewHeld")
		goto BrewThrow
	BrewThrow:
		TNT1 A 0 a_playsound("weapons/dynamitetoss")
		TNT1 A 0 a_firecustommissile("CWDM_brewGrenade")
		SBBF ABC 1
		TNT1 A 4
		REVV FA 2
		goto ready
	}
}

actor CWDM_EARLgun : CWDM_weapon
{
	//AttackSound "weapons/revolverfire"
	Tag "Flame gun"
	States
	{
	Spawn:
		BLHJ A -1
		Stop
	Ready:
		BLHJ A 1 A_WeaponReady
		Loop
	Select:
		BLHJ A 1 A_Raise
		Loop
	Deselect:
		BLHJ A 1 A_Lower
		Loop
	Fire:
		BLHJ A 4
		BLHJ A 0 A_Light2
		BLHJ A 0 A_Quake(3,5,0,5)
		BLHJ A 1 BRIGHT //A_FireBullets(2,2,2,50,"KWDMbulletPuff")
		BLHJ A 0 A_Light0
		BLHJ AA 8
		BLHJ A 0 A_Refire
		goto ready
	}
}

actor CWDM_smasherPunchProjectile 
{
    Damage 50
    Radius 32
    Height 32
    Speed 100
    Projectile
	Obituary "$OBSMASH"
	States
	{
	Spawn:
		TNT1 A 2
		stop
	}
}

// seems like projectileKickback doesn't seem to work?
// programming the knockback from the uppercut in ACS maybe be needed.
actor CWDM_smasherUppercutProjectile : CWDM_smasherPunchProjectile
{
	ProjectileKickback 10000
}

actor CWDM_smasherPunchOnce : Inventory
{
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 0
	+INVENTORY.UNDROPPABLE
}

actor CWDM_smasherWeapon : CWDM_weapon
{
	//AttackSound "weapons/revolverfire"
	Tag "Fists"
	States
	{
	Spawn:
		SSMW A -1
		Stop
	Ready:
		SSMW A 1 A_WeaponReady
		Loop
	Select:
		SSMW A 1 A_Raise
		Loop
	Deselect:
		SSMW A 1 A_Lower
		Loop
	AltFire:
		TNT1 A 0
		goto Running
	Running:
		TNT1 A 2 A_ChangeVelocity(2,0,0,CVF_RELATIVE)
		FIST A 0 A_Refire("Running")
		goto Uppercut
	Fire:
		TNT1 A 0 A_JumpIfInventory("CWDM_smasherPunchOnce",1,"PunchLeft")
		goto PunchRight
	PunchRight:
		TNT1 A 0 A_GiveInventory("CWDM_smasherPunchOnce", 1)
		TNT1 A 4
		SSMW DE 2
		TNT1 A 0 a_playsound("weapons/dynamitetoss")
		TNT1 A 2
		SSMW F 2 a_firecustommissile("CWDM_smasherPunchProjectile")
		SSMW G 6
		SSMW C 4
		goto ready
	PunchLeft:
		TNT1 A 0 A_TakeInventory("CWDM_smasherPunchOnce", 1)
		TNT1 A 4
		SSML AB 2
		TNT1 A 0 a_playsound("weapons/dynamitetoss")
		TNT1 A 2
		SSML C 2 a_firecustommissile("CWDM_smasherPunchProjectile")
		SSML D 6
		SSML B 4
		goto ready
	Uppercut:
		TNT1 A 5
		SSML A 2
		TNT1 A 0 a_playsound("weapons/dynamitetoss")
		TNT1 A 2
		SSMU B 2 a_firecustommissile("CWDM_smasherPunchProjectile")
		SSMU CA 8
		goto ready
	}
}