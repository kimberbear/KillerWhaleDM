actor KWDMmedikit : Health 1224
{
//$Category Health and Armor
//$Title "Medikit"
//$Sprite MEDIA0
	Inventory.Amount 50
	Inventory.MaxAmount 200
	Inventory.PickupSound "items/eat"
	Inventory.PickupMessage "Chowed down a bloody bucket of chum! +50 HP !"
	States
	{
	spawn:
		MEDI a -1
		stop
	}
}

actor KWDMmediumArmor : BasicArmorBonus 1225
{
//$Category Health and Armor
//$Title "Armor"
//$Sprite ARM1A0
	+INVENTORY.AUTOACTIVATE
	Inventory.PickupSound "items/armor1"
	Inventory.PickupMessage "Booze! +100 AP"
	Armor.SavePercent 75
	Armor.MaxSaveAmount 200
	Armor.SaveAmount 100

	States
	{
	spawn:
		ARM1 ab 4
		loop
	}
}

actor KWDMsmallHealthBonus : Health
{
    Inventory.Amount 25 // heals 25 HP
	Inventory.MaxAmount 200
}

actor KWDMsmallArmorBonus : BasicArmorBonus
{
    Armor.SaveAmount 25
	Armor.SavePercent 75
    Armor.MaxSaveAmount 200
}

actor KWDMdeathPack : CustomInventory
{
	Inventory.PickupSound "ammoget"
    Inventory.PickupMessage "+25 HP +25 AP"
    Inventory.Icon "BPAKA0"
    +INVENTORY.AUTOACTIVATE   // auto-use when picked up
	+INVENTORY.ALWAYSPICKUP
    +INVENTORY.NEVERRESPAWN   // optional: DM/Survival preference
	radius 20

    States
    {
	spawn:
		MEDI a -1
		stop
    Pickup:
        TNT1 A 0 A_GiveInventory("KWDMsmallHealthBonus", 1) // give health
        TNT1 A 0 A_GiveInventory("KWDMsmallArmorBonus", 1)  // give armor
        Stop
    }
}
actor QuadDamage : Powerup
{
    DamageFactor "Normal", 4
    Inventory.Icon "QUADC0"
}

actor KWDMskullOfPower : PowerupGiver
{
    Powerup.Type "QuadDamage"   // must match the actor *above*
    Powerup.Duration 1050
    Powerup.Color "Gold", 0.25

    +Inventory.AUTOACTIVATE
    +Inventory.FANCYPICKUPSOUND
    +FLOATBOB

    States
    {
    Spawn:
        QUAD ABCDEFGH 4 bright
        Loop
    }
}

actor KWDMskullOfPowerPickup : CustomInventory 1324
{
//$Category Powerup
//$Title "Quad Damage"
//$Sprite QUADC0
    +NOGRAVITY
    +INVENTORY.ALWAYSPICKUP
    Inventory.Pickupsound "powerup"
    Inventory.PickupMessage "Quad Damage!"
    Inventory.RespawnTics  2100 
	scale 2

    States
    {
    Spawn:
        QUAD ABCDEFGH 4 bright
        Loop

    Pickup:
        TNT1 A 0 A_GiveInventory("KWDMskullOfPower", 1)
        Stop
    }
}