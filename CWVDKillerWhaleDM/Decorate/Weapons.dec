ACTOR KWDMbulletPuff
{
  +NOBLOCKMAP
  +NOGRAVITY
  +ALLOWPARTICLES
  +RANDOMIZE
  RenderStyle Add
  Scale 0.2
  States
  {
  Spawn:
    HITS ABCDEFG 2 Bright
	Stop
  }
}

ACTOR KWDMweapon : Weapon 
{
	Weapon.BobRangeX 0.5
	Weapon.BobRangeY 0.5
	Weapon.BobSpeed 2
	Weapon.BobStyle Alpha
	Weapon.SelectionOrder 350
	Inventory.PickupSound "misc/usgpickup"
	+AMMO_OPTIONAL
}

ACTOR KWDMShotgun: KWDMweapon 1024
{
//$Category Weapon
//$Title "Shotgun"
//$Sprite SGIPA0
	Obituary "%o tasted %k's boomstick."
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	//Weapon.AmmoType "KWDMshell"
	Weapon.SlotNumber 2
	Xscale 0.5
	Yscale 0.5
	AttackSound "weapons/shotgunfire"
	Tag "Shotgun"
	States
	{
	Spawn:
		SGIP A -1
		Stop
	Ready:
		SHGN A 1 A_WeaponReady
		Loop
	Select:
		SHGN A 1 A_Raise
		Loop
	Deselect:
		SHGN A 1 A_Lower
		Loop
	Fire:
		SHGN B 0 A_Light2
		SHGN B 0 A_Quake(2,5,0,5)
		SHGN B 1 BRIGHT A_FireBullets(10,6,8,8,"KWDMbulletPuff")
		SHGN B 0 A_Light0
		SHGN CCCDDA 4
		Goto Ready
	}
}

actor KWDMrifleShotCount : Inventory
{
	Inventory.MaxAmount 24
	Inventory.InterHubAmount 0
	+INVENTORY.UNDROPPABLE
}

ACTOR KWDMrifle: KWDMweapon 1025
{
//$Category Weapon
//$Title "Rifle"
//$Sprite SGIPA0
	Obituary "%k opened fire on %o."
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	//Weapon.AmmoType "KWDMbullet"
	Weapon.SlotNumber 2
	Xscale 0.5
	Yscale 0.5
	AttackSound "weapons/riflefire"
	Tag "Rifle"
	States
	{
	Spawn:
		SGIP A -1
		Stop
	Ready:
		RFLE A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Select:
		RFLE A 1 A_Raise
		Loop
	Deselect:
		RFLE A 1 A_Lower
		Loop
	Fire:
		RFLE A 0 A_JumpIfInventory("KWDMrifleShotCount",24,"Reload")
		RFLE A 0 A_GiveInventory("KWDMrifleShotCount", 1)
		RFLE B 0 A_Light2
		RFLE B 0 A_Quake(1,5,0,5)
		RFLE B 1 BRIGHT A_FireBullets(2,2,1,10,"KWDMbulletPuff")
		RFLE B 0 A_Light0
		RFLE CCA 1
		RFLE A 0 A_ReFire
		Goto Ready
	Reload:
		RFLE A 0 A_TakeInventory("KWDMrifleShotCount",24)

		RFLE ADEFGHI 2
		TNT1 A 0 A_PlaySound("weapons/weaponopen")
		RFLE JKLMNOP 2
		TNT1 A 0 A_PlaySound("weapons/weaponclose")
		RFLE QRST 2
		goto ready
	}
}

Actor FlareProjectile
{
    Damage 20
    Radius 8
    Height 8
    Speed 70
    Projectile
    Obituary "%o was killed by a flare."

    States
    {
    Spawn:
        FLAR A 1 BRIGHT //A_SpawnItem("WynSmokeTrail") //this was causing lag as it's super buggy and removed now
        Loop

    Death:
        TNT1 A 0 A_Quake(3, 4, 0, 1024, "dsnone")
        TNT1 A 0 A_Quake(2, 8, 0, 1024, "dsnone")
        TNT1 A 0 A_Quake(1, 12, 0, 1024, "dsnone")
        //TNT1 A 0 A_SpawnItem("WynSmallExplosion") //this was causing lag as it's super buggy and removed now
        TNT1 A 25 A_PlaySound("whale/flaregunexplode", CHAN_5)
        Stop
    }
}

ACTOR KWDMgrenadeLauncher: KWDMweapon 1026
{
//$Category Weapon
//$Title "Grenade Launcher"
//$Sprite SGIPA0
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	//Weapon.AmmoType "KWDMshell"
	Weapon.SlotNumber 2
	Xscale 0.5
	Yscale 0.5
	AttackSound "weapons/grenadefire"
	Tag "Grenade Launcher"
	States
	{
	Spawn:
		SGIP A -1
		Stop
	Ready:
		GGUN A 1 A_WeaponReady
		Loop
	Select:
		GGUN A 1 A_Raise
		Loop
	Deselect:
		GGUN A 1 A_Lower
		Loop
	Fire:
		GGUN B 0 A_Light2
		GGUN B 0 A_Quake(2,5,0,5)
		FLAR B 0 A_PlayWeaponSound("weapons/grenadefire")
		GGUN B 1 a_firecustommissile("grenade")
		GGUN B 0 A_Light0
		GGUN BBCCA 6
		Goto Ready
	}
}

ACTOR KWDMflare: KWDMweapon 1027
{
//$Category Weapon
//$Title "Flare"
//$Sprite SGIPA0
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	//Weapon.AmmoType "KWDMshell"
	Weapon.SlotNumber 2
	Xscale 0.5
	Yscale 0.5
	AttackSound "weapons/grenadefire"
	Tag "Flare gun"
	States
	{
	Spawn:
		SGIP A -1
		Stop
	Ready:
		FLGN A 1 A_WeaponReady
		Loop
	Select:
		FLGN A 1 A_Raise
		Loop
	Deselect:
		FLGN A 1 A_Lower
		Loop
	Fire:
		FLGN B 0 A_Light2
		FLGN B 0 A_Quake(2,5,0,5)
		FLGN B 0 A_PlayWeaponSound("weapons/flarefire")
		FLGN B 1 a_firecustommissile("FlareProjectile")
		FLGN B 0 A_Light0
		FLGN BBCCA 6
		Goto Ready
	}
}

actor KWDM1887firedOnce : Inventory
{
	Inventory.MaxAmount 1
	Inventory.InterHubAmount 0
	+INVENTORY.UNDROPPABLE
}

actor KWDMdual1887: KWDMweapon
{
	AttackSound "weapons/shotgunfire"
	Tag "Dual 1887s"
	States
	{
	Spawn:
		DWLA A -1
		Stop
	Ready:
		DWLA A 1 A_WeaponReady
		Loop
	Select:
		DWLA A 1 A_Raise
		Loop
	Deselect:
		DWLA A 1 A_Lower
		Loop
	Fire:
		DWLA A 0 A_JumpIfInventory("KWDM1887firedOnce",1,"FireRight")
		DWLA A 1
		Goto FireLeft
	FireLeft:
		DWLA A 0 A_GiveInventory("KWDM1887firedOnce", 1)
		DWLA A 0 A_Light2
		DWLA A 0 A_Quake(2,5,0,5)
		DWLL A 2 BRIGHT A_FireBullets(4,4,5,8,"KWDMbulletPuff")
		DWLL B 0 A_Light0
		DWLL B 4
		DWLL DEDE 2
		DWLL BC 4
		goto Ready
	FireRight:
		DWLA A 0 A_TakeInventory("KWDM1887firedOnce", 1)
		DWLA A 0 A_Light2
		DWLA A 0 A_Quake(2,5,0,5)
		DWLR A 2 BRIGHT A_FireBullets(4,4,5,8,"KWDMbulletPuff")
		DWLR A 0 A_Light0
		DWLR B 4
		DWLR DEDE 2
		DWLR BC 4
		goto Ready
	}
}